plugins {
    id 'com.modrinth.minotaur' version '2.+'
}

archivesBaseName = project.archives_base_name + "_api"
group = rootProject.group
version = project.api_version

loom {
    accessWidenerPath = file("src/main/resources/server_mobs_api.accesswidener")
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }

    exclude "**/*.xcf"
    exclude "**/*sync-conflict*"
    exclude "/tools"
    exclude "**/unused"
}

if (System.getenv("MODRINTH_TOKEN")) {
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = "5yRAzwig"
        versionNumber = "" + api_version
        versionType = "alpha"
        uploadFile = remapJar
        gameVersions = [((String) project.minecraft_version)]
        loaders = ["fabric"]
        dependencies {
            required.project("fabric-api")
            embedded.project("holograms")
            embedded.project("polymer")
            optional.project("polypack_host")
        }
    }
}